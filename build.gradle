plugins {
    id "java-library"
    id "maven-publish"
}

allprojects {
    apply plugin: "java-library"

    group = project.maven_group
    version = project.release_version

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile).configureEach { tsk ->
        tsk.options.encoding = "UTF-8"
    }

    tasks.withType(Test).configureEach { tsk ->
        tsk.systemProperty "file.encoding", "UTF-8"
    }

    jar {
        manifest {
            attributes "Implementation-Version": project.version
        }
    }

    compileJava.dependsOn clean
}

subprojects {
    apply plugin: "maven-publish"

    base {
//        archivesName = rootProject.archives_base_name + "-" + project.name
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:26.0.1"
        annotationProcessor(compileOnly("org.projectlombok:lombok:1.18.36"))

        testImplementation platform('org.junit:junit-bom:5.11.4')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testAnnotationProcessor(testCompileOnly("org.projectlombok:lombok:1.18.36"))
    }

    test {
        useJUnitPlatform()

        ignoreFailures true

        testLogging {
            exceptionFormat = "full"
            events "passed", "skipped", "failed"
        }
    }

    publishing {
        publications {
            create("${project.name}Maven", MavenPublication) {
                from components.java
                artifactId = project.name
            }
        }

        repositories {
            mavenLocal()
        }
    }
}
